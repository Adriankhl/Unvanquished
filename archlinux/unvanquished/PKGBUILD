# Maintainer: Viech <viech unvanquished net>
# Contributor: Gereon Schomber
# Contributor: Martin F. Schumann

pkgname=unvanquished
pkgver=0.24.0
pkgrel=2
pkgdesc='A team-based, fast-paced, fps/rts hybrid game which pits aliens against humans. Monthly alpha release.'
arch=('x86_64' 'i686')
url='http://www.unvanquished.net'
license=('GPL3')
makedepends=('git' 'cmake')
depends=('zlib' 'gmp' 'nettle' 'geoip' 'curl' 'sdl2' 'glew' 'libpng'
         'libjpeg-turbo' 'libwebp>=0.2.0' 'freetype2' 'openal' 'libogg'
         'libvorbis' 'speex' 'libtheora' 'opusfile' 'ncurses' 'xdg-utils'
         'desktop-file-utils' 'shared-mime-info' 'hicolor-icon-theme')
provides=('unvanquished')
conflicts=('unvanquished-maps' 'unvanquished-git')
options=('emptydirs')
backup=('etc/unvanquished/config/server.cfg' 'etc/unvanquished/config/maprotation.cfg')
changelog='ChangeLog'
install='unvanquished.install'
source=("unvanquished::git+https://github.com/Unvanquished/Unvanquished.git#tag=${pkgver}-${pkgrel}-arch"
        'ChangeLog' 'unvanquished.install')

build() {
	cd "$srcdir"/unvanquished

	cmake -D BUILD_GAME_QVM=OFF -D BUILD_TTY_CLIENT=ON .
	make
}

package() {
	# create installation directories
	cd "$pkgdir"

	install -dm755 etc/conf.d \
	               etc/unvanquished/config \
	               usr/bin \
	               usr/lib/systemd/system \
	               usr/lib/unvanquished \
	               usr/share/applications \
	               usr/share/icons/hicolor/128x128/apps \
	               usr/share/licenses/unvanquished \
	               var/cache/unvanquished/update-paks \
	               var/lib/unvanquished/pkg \
	               var/lib/unvanquished-server/config

	# install content
	cd "$srcdir"/unvanquished

	install -m 755 daemon* *.so download-pk3.sh "$pkgdir"/usr/lib/unvanquished/
	install -m 644 debian/unvanquished.png      "$pkgdir"/usr/share/icons/hicolor/128x128/apps/
	install -m 644 COPYING.txt                  "$pkgdir"/usr/share/licenses/unvanquished/
	install -m 644 debian/configfiles/*         "$pkgdir"/etc/unvanquished/config/

	# install starters
	cd "$srcdir"/unvanquished/archlinux

	install -m 755 unvanquished.sh              "$pkgdir"/usr/bin/unvanquished
	install -m 755 unvanquished-tty.sh          "$pkgdir"/usr/bin/unvanquished-tty
	install -m 755 unvanquished-update-paks.sh  "$pkgdir"/usr/bin/unvanquished-update-paks
	install -m 644 unvanquished.conf            "$pkgdir"/etc/conf.d/
	install -m 644 unvanquished.service         "$pkgdir"/usr/lib/systemd/system/
	install -m 644 unvanquished.desktop         "$pkgdir"/usr/share/applications/

	# setup server home directory
	cd "$pkgdir"/var/lib/unvanquished-server/config

	ln -s ../../../../etc/unvanquished/config/* .
}

md5sums=('SKIP'
         '688dc19586040b4c02852750be353513'
         '9ce3867ed35be48ffcafb8a713172385')
